###############################################################################
# FILE
#       global.env
#
# DESCRIPTION
#     This file sets environment variables global to all users of the 
#     CTBT Monitoring System (CMS). The user can supply the following
#     variables (defaults are set in section USER VARIABLES below):
#           CMS_RELEASE     - release label of the CMS software
#           CMS_HOME        - directory of CMS source code tree
#           CMS_SCRIPTS     - directory of CMS scripts tree
#           CMS_CONFIG      - directory of CMS configuration tree
#	    RN_HOME         - directory of Radionuclide binaries
#           RN_SCRIPTS      - directory of Radionuclide scripts tree
#           RN_CONFIG       - directory of Radionuclide configuration files 
#           CMS_MODE        - user mode (process, analysis)
#           IMSPAR          - top level, mode specific config file
#
#     Note about CMS_CONFIG and IMSPAR:
#     ---------------------------------
#     Usually IMSPAR is derived from CMS_CONFIG and CMS_MODE but
#     not when CMS_CONFIG2 is set. In this case IMSPAR is derived
#     from CMS_CONFIG2 and CMS_MODE thus allowing users to obtain
#     the database connect strings from the runtime system even when
#     CMS_CONFIG is set to the ClearCase VOB (which has only fake
#     process.par, analysis.par, ...  files). The proposed usage for
#     developers is e.g.
#           setenv CMS_MODE    process
#           setenv CMS_CONFIG  /$lan/software/shi/config
#           setenv CMS_CONFIG2 ANY_OTHER_CONFIG_LOCATION
#
#     Note about CMS_SCRIPTS:
#     -----------------------
#     These variables have second variants to be used as alternative
#     search paths, i.e. developers can do something like
#           setenv CMS_SCRIPTS   /$lan/software/shi/scripts
#           setenv CMS_SCRIPTS2  ANY_OTHER_SCRIPT_LOCATION
#     This causes $CMS_SCRIPTS/bin to be searched before $CMS_SCRIPTS2/bin,
#     thus allowing developers to put their newly created versions into
#     /vobs/idc/scripts/bin while all the other scripts are still found
#     in /${lan}/software/shi/scripts. This feature applies to the search
#     paths $CMS_SCRIPTS/bin and $CMS_SCRIPTS/lib.
#
# AUTHOR
#     Michael O'Brien - 03/18/98
#     Martin Ertl     - 2004
#     Tim Hampton    30-May-2005
#
# OWNWER
#
# MODIFICATIONS
#     Changes for Linux environment          TH/IDC  30-May-2005
#     Merged with Solaris environment        AB/AWST 02-Mar-2006
#     Modified for Radionuclide monitoring   JW/AWST 15-Jul-2009
#     Added env variable for norfy           JW/AWST 31-Dec-2010
#     Clean up , simplfy & Unify             SL-IDC/APS xx-Dec-2016
###############################################################################

################ This code is added to source LAN specifics Env. Variables ####

if (-e ${CMS_CONFIG}/system_specs/env/lan.env ) then
  	source ${CMS_CONFIG}/system_specs/env/lan.env
else
	echo "Warning: filename ${CMS_CONFIG}/system_specs/env/lan.env not found."
endif

###################   End of LAN specifics Env. Variables  ####################
set release=`uname -r`
setenv HOME			`echo ~`
### BEGIN USER VARIABLES -------------------- Default Settings ----------------

## CTBT Monitoring System, SHI
if ( ! $?CMS_HOME )       setenv CMS_HOME     /${lan}/software/shi/rel
if ( ! $?SHARED_HOME )    setenv SHARED_HOME  /${lan}/software/shared
if ( ! $?CMS_SCRIPTS )    setenv CMS_SCRIPTS  /${lan}/software/shi/scripts
if ( ! $?CMS_CONFIG )     setenv CMS_CONFIG   /${lan}/software/shi/config
if ( ! $?SHI_AUX )        setenv SHI_AUX      /${lan}/software/shi/aux

## CTBT Monitoring System, RN
if ( ! $?RN_CONFIG )      setenv RN_CONFIG    /${lan}/software/rn/config
if ( ! $?RN_HOME )        setenv RN_HOME      /${lan}/software/rn/rel
if ( ! $?RN_SCRIPTS )     setenv RN_SCRIPTS   /${lan}/software/rn/scripts

## CMS_MODE and IMSPAR
if ( ! $?CMS_MODE )       setenv CMS_MODE     public
set mode = $CMS_MODE
if ( $CMS_MODE == dual )  set mode = process
if ( $?CMS_CONFIG2 ) then
    setenv IMSPAR         ${CMS_CONFIG2}/system_specs/${mode}.par
else
    setenv IMSPAR         ${CMS_CONFIG}/system_specs/${mode}.par
endif

# Worldwind
setenv WORLDWIND_HOME /cots/WorldWind/1.5.1

### END USER VARIABLES --------------------------------------------------------

## Test user supplied variables
if ( ! -d $CMS_HOME )         echo "Warning: CMS_HOME directory not found: $CMS_HOME"
if ( ! -d $CMS_SCRIPTS )      echo "Warning: CMS_SCRIPTS directory not found: $CMS_SCRIPTS"
if ( ! -d $CMS_CONFIG )       echo "Warning: CMS_CONFIG directory not found: $CMS_CONFIG"
if ( ! -d $RN_HOME )         echo "Warning: RN_HOME directory not found: $RN_HOME"
if ( ! -d $RN_SCRIPTS )      echo "Warning: RN_SCRIPTS directory not found: $RN_SCRIPTS"
if ( ! -d $RN_CONFIG )       echo "Warning: RN_CONFIG directory not found: $RN_CONFIG"
if ( ! -r $IMSPAR )           echo "Warning: No par file or file not readable: ${IMSPAR}"
if ( $?CMS_SCRIPTS2 ) then
    if ( ! -d $CMS_SCRIPTS2 ) echo "Warning: CMS_SCRIPTS2 directory not found: $CMS_SCRIPTS2"
endif

# using isaexec-like method to build platform-specific variables
setenv os `uname -s`
setenv isa `uname -p`

## Oracle
setenv ORACLE_HOME		/cots/oracle/c201.oic
## RabbitMQ
setenv RABBITMQ_C_HOME          /cots/RabbitMQ/rabbitmq-c/0.5.0
## Java
setenv JAVA_HOME                /cots/oracle/java/jdk1.8
# ANT
setenv ANT_HOME /git/apache-ant-1.9.10/dist

setenv ORACLE_SID		oracle
setenv TNS_ADMIN		/cots/oracle/conf
setenv NLS_LANG 		.al32utf8

#### start_tlisten puts this into the env
setenv LANG                     C
if ( $CMS_MODE == process ) then
    setenv IPC_GROUP		DACS
else
    setenv IPC_GROUP		INTERACTIVE
endif
setenv APPDIR			/tmp:$CMS_HOME/bin

## Threshold Monitoring
setenv TM_HOME		${CMS_CONFIG}/app_config/monitoring/TM

## Perl
setenv PERL5LIB         ${CMS_SCRIPTS}/lib
setenv PERL5LIB 	${PERL5LIB}:${CMS_SCRIPTS}/lib/perl5/site_perl

# In case of RH6
setenv PERL5LIB		${PERL5LIB}:${SHI_AUX}/perl/lib/perl5
setenv GMT_SHAREDIR  		/usr/share/GMT
setenv XLIB_SKIP_ARGB_VISUALS 1

## Perl, add system packages only at the end here
setenv PERL5LIB         ${PERL5LIB}:/usr/lib64/perl5

setenv PERLPATH		/usr/bin/
setenv PERL		/usr/bin/perl

## needed to enable input fields of Perl Tk GUIs
unsetenv XMODIFIERS

## Python
setenv PYTHONPATH $SHI_AUX/lib/python2.6/site-packages:$CMS_HOME/lib/python

###########################################
##   Derived and Universal Environment   ##
###########################################

## Paths to Executables
setenv PATH		$JAVA_HOME/bin:${ANT_HOME}/bin
setenv PATH		${PATH}:/bin
setenv PATH		${PATH}:/usr/bin
setenv PATH		${PATH}:/usr/sbin
setenv PATH		${PATH}:${CMS_SCRIPTS}/bin
if ( $?CMS_SCRIPTS2 ) then
   setenv PATH		${PATH}:${CMS_SCRIPTS2}/bin
endif
if ( $CMS_MODE == analysis ) then
   setenv PATH          ${PATH}:${SHI_AUX}/bin/analysis
endif
setenv PATH             ${PATH}:${RN_SCRIPTS}/bin
setenv PATH		${PATH}:${CMS_HOME}/bin
setenv PATH             ${PATH}:${RN_HOME}/bin
setenv PATH		${PATH}:${ORACLE_HOME}/bin
setenv PATH		${PATH}:${RABBITMQ_C_HOME}/bin
setenv PATH		${PATH}:${SHI_AUX}/bin
setenv PATH		${PATH}:/etc
setenv PATH		${PATH}:.

## Paths to Run-Time Libraries
setenv LD_LIBRARY_PATH	${ORACLE_HOME}/lib
setenv LD_LIBRARY_PATH	${LD_LIBRARY_PATH}:${CMS_HOME}/lib/${os}-${release}-${isa}
setenv LD_LIBRARY_PATH	${LD_LIBRARY_PATH}:${RABBITMQ_C_HOME}/lib64
setenv LD_LIBRARY_PATH	${LD_LIBRARY_PATH}:${WORLDWIND_HOME}
setenv LD_LIBRARY_PATH	${LD_LIBRARY_PATH}:${CMS_SCRIPTS}/lib
if ( $?CMS_SCRIPTS2 ) then
   setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:${CMS_SCRIPTS2}/lib
endif
setenv LD_LIBRARY_PATH	${LD_LIBRARY_PATH}:/usr/lib

## XFILESEARCHPATH
setenv XFILESEARCHPATH	                   ${CMS_HOME}/X11/%T/%N%S
setenv XFILESEARCHPATH	${XFILESEARCHPATH}:${CMS_CONFIG}/system_specs/app-resources/%T/%N%S

## XBMLANGPATH required by norfy
setenv XBMLANGPATH ${CMS_CONFIG}/system_specs/app-resources/%B

## XAPPLRESDIR
setenv XAPPLRESDIR	${CMS_CONFIG}/system_specs/app-resources/%T/%N%S
setenv XAPPLRESDIR	${XAPPLRESDIR}:${HOME}/app-resources/%T/%N%S

## MANPATH
setenv MANPATH		/usr/man
setenv MANPATH		${MANPATH}:/usr/share/man
setenv MANPATH		${MANPATH}:${CMS_SCRIPTS}/doc/man
setenv MANPATH		${MANPATH}:${CMS_HOME}/man
setenv MANPATH		${MANPATH}:${CMS_HOME}/doc/man
setenv MANPATH          ${MANPATH}:${RN_SCRIPTS}/doc/man
setenv MANPATH          ${MANPATH}:${RN_HOME}/man
setenv MANPATH          ${MANPATH}:${RN_HOME}/doc/man
setenv MANPATH          ${MANPATH}:${RABBITMQ_C_HOME}/share/man
setenv MANPATH          ${MANPATH}:${SHI_AUX}/man

## SCHEMEPATH
setenv SCHEMEPATH	$CMS_HOME/scheme

## DACS servers need GDI_HOME as environment variable
setenv GDIHOME		${CMS_HOME}
setenv GDI_HOME		${GDIHOME}

## used for the generic .cshrc
setenv CMS_APPS		$CMS_CONFIG/app_config

## analyst default printer
if ( $CMS_MODE == analysis ) then
   setenv PRINTER lj5000-014
   setenv LPDEST  lj5000-014
endif

## geotool configuration
setenv GEOTOOL_HOME	${CMS_CONFIG}/geotool
setenv GEOTOOL_PLUGINS	${CMS_HOME}/lib/${os}-${release}-${isa}/plugins

## NETVISA home
setenv NETVISA_HOME /`imspar lan`/software/shi/netvisa

## Write core dumps for debugging
limit coredumpsize unlimited

####  DTKGPMCC
setenv DTKGPMCC_DRIVERS ${CMS_HOME}/lib/${os}-${release}-${isa}
setenv DTKGPMCC_RESOURCES ${CMS_HOME}/matlab/dtkpmcc/
